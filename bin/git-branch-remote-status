#!/usr/bin/env zsh
# Check the status of the current branch against its remote counterpart.
# Usage:
# $ git-branch-remote-status
# $ git-branch-remote-status branch
#

local branchName=$1
if [[ "$branchName" = '' ]]; then
  branchName="$(git branch-current)"
fi

local remoteHead=$(git rev-parse $branchName@{upstream} 2>/dev/null)
# Remote has no hash, means it never get pushed
if [[ "$remoteHead" = '' ]]; then
  echo "local_never_pushed"
  exit
fi

local localHead=$(git rev-parse $branchName@{0} 2>/dev/null)
# Local is same as remote
if [[ "$remoteHead" = "$localHead" ]]; then
  echo "local_identical"
  exit
fi

local remoteMergeBase=$(git merge-base $branchName@{0} $branchName@{upstream} 2>/dev/null)
# Merge base is the same as remote, it means we are ahead
if [[ "$remoteHead" = "$remoteMergeBase" ]]; then
  echo "local_ahead"
  exit
fi

# Merge base is the same as local, it means we are behind
if [[ "$localHead" = "$remoteMergeBase" ]]; then
  echo "local_behind"
  exit
fi

# Any other case means we've diverged
echo "local_diverged"
